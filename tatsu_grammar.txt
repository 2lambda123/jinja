start
    =
    expressions
    $
    ;

expressions
    =
    {expression}*
    ;

expression
    =
    content | raw_block_expression | block_expression | variable_expression | comment_expression
    ;

raw_block_expression
    =
    raw_block_start
    { !raw_block_end CHAR }*
    raw_block_end
    ;

raw_block_start
    =
    block_open "raw" block_close
    ;

raw_block_end
    =
    block_open "endraw" block_close
    ;

block_expression
    =
    block_start {SP}* expressions {SP}* block_end
    | block_start
    ;

block_start
    =
    block_open !("end") IDENTIFIER {block_parameter}* block_close
    ;

block_end
    =
    block_open "end" IDENTIFIER block_close
    ;

block_open
    =
    @:"{%" {SP}*
    ;
block_close
    =
    {SP}* @:"%}"
    ;

block_parameter
    =
    {SP}+ @:(IDENTIFIER "=" block_parameter_value)
    | {SP}+ @:block_parameter_value
    ;
block_parameter_value
    =
    {ALPHA}+
    ;

variable_expression
    =
    variable_open IDENTIFIER { !variable_close variable_filter }* variable_close
    ;
variable_open
    =
    @:"{{" {SP}*
    ;
variable_close
    =
    {SP}* @:"}}"
    ;

variable_filter
    =
    {SP}* "|" {SP}* @:filter
    ;
filter =
    @+:IDENTIFIER
    [@+:filter_parameters]
    ;

filter_parameters
    =
    "("
        @:[filter_parameter_list]
    ")"
    ;
filter_parameter_list
    =
    @+:filter_parameter {filter_parameter_separator ~ @+:filter_parameter}*
    ;

filter_parameter_separator
    =
    {SP}* "," {SP}*
    ;
filter_parameter        = {!"," !")" CHAR}* ;

comment_expression      = comment_open comment_content comment_close ;
comment_open            = "{#" ;
comment_close           = "#}" ;
comment_content         = {!comment_close CHAR}* ;

content                 = !(block_open | variable_open | comment_open) CHAR ;

IDENTIFIER
    =
    /[a-zA-Z][a-zA-Z0-9]*/
    ;

ALPHA
    =
    /[a-zA-Z]/
    ;

DIGIT
    =
    /[0-9]/
    ;

SP
    =
    /\s/
    ;

CHAR
    =
    ?'.' | ?'\s'
    ;