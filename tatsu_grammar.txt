start
    =
    expressions
    $
    ;

expressions
    =
    {expression}*
    ;

expression
    =
    content | raw_block_expression | block_expression | variable_expression | comment_expression
    ;

raw_block_expression
    =
    raw_block_start
    { !raw_block_end CHAR }*
    raw_block_end
    ;

raw_block_start
    =
    block_open "raw" block_close
    ;

raw_block_end
    =
    block_open "endraw" block_close
    ;

block_expression
    =
    block_start {SP}* expressions {SP}* block_end
    | block_start
    ;

block_start
    =
    block_open !("end") IDENTIFIER {block_parameter}* block_close
    ;

block_end
    =
    block_open "end" IDENTIFIER block_close
    ;

block_open
    =
    @:"{%" {SP}*
    ;
block_close
    =
    {SP}* @:"%}"
    ;

block_parameter
    =
    variable_accessor_call_parameter
    ;

variable_expression
    =
    variable_open variable_identifier { !variable_close variable_filter }* variable_close
    ;
variable_open
    =
    @:"{{" {SP}*
    ;
variable_close
    =
    {SP}* @:"}}"
    ;

variable_identifier
    =
    ( IDENTIFIER | STRING )
    { variable_accessor }*
    ;

variable_accessor
    =
    variable_accessor_brackets | variable_accessor_call | variable_accessor_dot
    ;

variable_accessor_brackets
    =
    "[" variable_identifier "]"
    ;

variable_accessor_call
    =
    "(" [variable_accessor_call_parameters] ")"
    ;

variable_accessor_call_parameters
    =
    @+:variable_accessor_call_parameter
    { "," @+:variable_accessor_call_parameter }*
    ;

variable_accessor_call_parameter
    =
    variable_accessor_call_parameter_key_value
    | variable_accessor_call_parameter_value_only
    ;

variable_accessor_call_parameter_key_value
    =
    {SP}* key:IDENTIFIER {SP}* "=" value:variable_accessor_call_parameter_value {SP}*
    ;

variable_accessor_call_parameter_value_only
    =
    value:variable_accessor_call_parameter_value
    ;

variable_accessor_call_parameter_value
    =
    {SP}* @:variable_identifier {SP}*
    ;

variable_accessor_dot
    =
    "." variable_identifier
    ;

variable_filter
    =
    {SP}* "|" {SP}* @:filter
    ;
filter =
    @+:IDENTIFIER
    [@+:filter_parameters]
    ;

filter_parameters
    =
    variable_accessor_call
    ;

comment_expression      = comment_open comment_content comment_close ;
comment_open            = "{#" ;
comment_close           = "#}" ;
comment_content         = {!comment_close CHAR}* ;

content                 = !(block_open | variable_open | comment_open) CHAR ;

STRING
    =
    SINGLE_QUOTE_STRING | DOUBLE_QUOTE_STRING
    ;

SINGLE_QUOTE_STRING
    =
    "'" { !"'" ?'.' }* "'"
    ;

DOUBLE_QUOTE_STRING
    =
    '"' { !'"' ?'.' }* '"'
    ;

IDENTIFIER
    =
    /[a-zA-Z_][a-zA-Z0-9_]*/
    ;

ALPHA
    =
    /[a-zA-Z]/
    ;

DIGIT
    =
    /[0-9]/
    ;

SP
    =
    /\s/
    ;

CHAR
    =
    ?'.' | ?'\s'
    ;